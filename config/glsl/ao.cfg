////////////////////////////////////////////////
//
// ambient obscurance
//
////////////////////////////////////////////////

ambientobscurancevariantshader = [
    lineardepth = (>= (strstr $arg2 "l") 0)
    packeddepth = (>= (strstr $arg2 "p") 0)

    shader_new 0 $arg1 [
        shader_define AO_LINEAR_DEPTH $lineardepth
        shader_define AO_DEPTH_FORMAT $aodepthformat
        shader_define AO_PACKED_DEPTH $packeddepth
        shader_define AO_TAPS         $arg3

        local gdepth_msaa gdepth_packed
        gdepth_msaa   = (&& $msaasamples [! $lineardepth])
        gdepth_packed = (&& $lineardepth [! $aodepthformat])

        shader_gdepth_support "tex0" $gdepth_msaa $gdepth_packed $lineardepth
        shader_gbuf_support "tex1" $msaasamples
        shader_include_vs "config/glsl/shared/screencoord.vert"
        shader_source "config/glsl/ao/ao.vert" "config/glsl/ao/ao.frag"
    ]
]

ambientobscuranceshader = [
    ambientobscurancevariantshader (format "ambientobscurance%1%2" $arg1 $arg2) $arg1 $arg2
]

shader_new 0 "linearizedepth" [
    shader_define AO_DEPTH_FORMAT $aodepthformat
    shader_gdepth_support "tex0" $msaasamples
    shader_include_vs "config/glsl/shared/screencoord.vert"
    shader_source "config/glsl/ao/linearizedepth.vert" "config/glsl/ao/linearizedepth.frag"
]

bilateralvariantshader = [
    linear = (>= (strstr $arg2 "l") 0)
    packed = (>= (strstr $arg2 "p") 0)
    upscaled = (>= (strstr $arg2 "u") 0)
    numtaps = $arg3
    reduced = $arg4
    filterdir = $arg5

    shader_new 0 $arg1 [
        shader_define AO_DEPTH_FORMAT   $aodepthformat
        shader_define AO_LINEAR_DEPTH   $linear
        shader_define AO_PACKED_DEPTH   $packed
        shader_define AO_UPSCALED       $upscaled
        shader_define AO_REDUCED        $reduced
        shader_define AO_FILTER_TAPS    $numtaps
        shader_define AO_FILTER_DIR_X   0
        shader_define AO_FILTER_DIR_Y   1
        shader_define AO_MIN_TEX_OFFSET $mintexoffset
        shader_define AO_MAX_TEX_OFFSET $maxtexoffset

        if (=s $filterdir "x") [
            shader_define AO_FILTER_DIR AO_FILTER_DIR_X
        ] [
            shader_define AO_FILTER_DIR AO_FILTER_DIR_Y
        ]

        shader_gdepth_support "tex1" (&& $msaasamples [! $linear])
        shader_include_vs "config/glsl/shared/screencoord.vert"
        shader_source "config/glsl/ao/bilateral.vert" "config/glsl/ao/bilateral.frag"
    ]
]

bilateralshader = [
    bilateralvariantshader (format "bilateralx%1%2" $arg1 $arg2) $arg1 $arg2 $arg3 x
    bilateralvariantshader (format "bilateraly%1%2" $arg1 $arg2) $arg1 $arg2 $arg3 y
]
