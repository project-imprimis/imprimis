///////////////////////////////////////////////////////////////////////////////
//  Scoreboard                                                               //
///////////////////////////////////////////////////////////////////////////////
newui "scoreboard" [
    if $mainmenu [hideui "scoreboard"]
    if (= $getmode 0) [hideui "scoreboard"] //no scoreboard in edit
    uiallowinput 0
    refreshscoreboard
    uicolor (? $sbtransparent $c_menu_a $c_menu) 0 0 [
        uivlist 0 [
            UI_sbheader
            uifill 0 $pad_thin
            UI_playertableteam
            UI_spectatorlist
        ]
    ]
] [if $mainmenu [hideui "scoreboard"]]

///////////////////////////////////////////////////////////////////////////////

UI_sbheader = [
    uifill 0 $pad_thin
    uifill $sbwidth $pad_medium [
        uifill (-f $sbwidth (? (= $showip 1) $sbipwidth 0)) 0 [
            uialign -1
            uiclip (-f $sbwidth (? (= $showip 1) $sbipwidth 0)) 0 [
                uialign -1
                uispace $pad_thin 0 [
                    uivlist 0 [
                        if $scoreboardservinfo [
                            uitext $scoreboardservinfo $title_height
                        ] [
                            uitext "^f4Imprimis" $title_height
                        ]
                        uihlist $sbpadhorizontal [
                            uifill
                            uihlist $pad_thin [
                                uitext "^fs[" $sbmidtext
                                uitext (concatword (at ["^f0" "^f0" "^f2" "^f3"] $getmastermode) (getmastermodename $getmastermode)) $sbmidtext
                                uitext "^fS]" $sbmidtext
                            ]
                            UItriangle $color_light_gray $trianglewidth $triangleheight 270
                            uitext (getmodeprettyname $getmode) $sbmidtext
                            UItriangle $color_light_gray $trianglewidth $triangleheight 270
                            uitext $scoreboardmap $sbmidtext
                            if (MODE_TIMED $getmode) [
                                UItriangle $color_light_gray $trianglewidth $triangleheight 270
                                uitext (concatword (? (|| $intermission $paused) "^f3" "^f8") $scoreboardtime) $sbmidtext
                            ]
                        ]
                        uialign* -1
                    ]
                ]
            ]
        ]
        if $scoreboardmultiplayer [
            if $showip [
                uifill $sbipwidth 0 [
                    uialign 1
                    UIbar 0 1; uialign- -1
                    uiclip $sbipwidth 0 [
                        uialign 1
                        uispace $pad_thin 0 [
                            uivlist $pad_thin [
                                uicolortext $connectedip $color_mid_gray $sbmidtext
                                uicolortext $connectedport $color_mid_gray $sbmidtext
                                uialign* 1
                            ]
                        ]
                        uialign- 1
                    ]
                ]
            ]
        ]
    ]
]
//-------------------------------------------------------------------------------------------------

UI_playertableteam = [
    uifill 0 0 [
        uihlist 0 [
            uitable 0 0 [
                uialign 0 -1
                uitableheader [
                    uifill $sbpadhorizontal
                    if $scoreboardmultiplayer [
                        if $showclientnum [uifill $sbcolumnwidth $sbtableheaderheight [uicolortext "#"     $color_mid_gray $sblittletext ; uialign- -1 1]]
                        if $showping      [uifill $sbcolumnwidth $sbtableheaderheight [uicolortext "P"     $sbnetstat $sblittletext ; uialign- -1 1]]
                        if $showpj        [uifill $sbcolumnwidth $sbtableheaderheight [uicolortext "PJ"    $sbnetstat $sblittletext ; uialign- -1 1]]
                    ]
                    if $showdeaths       [uifill $sbcolumnwidth  $sbtableheaderheight [uicolortext "D"     $sbdeaths $sblittletext ; uialign- -1 1]]
                    if $showkills        [uifill $sbcolumnwidth  $sbtableheaderheight [uicolortext "K"     $sbkills $sblittletext ; uialign- -1 1]]
                    if $showscore        [uifill $sbcolumnwidth  $sbtableheaderheight [uicolortext "Score" $sbscore $sblittletext ; uialign- -1 1]]
                    uifill (sbnamecolwidth) $sbtableheaderheight [uicolortext (getteamscore 1) 0x4060D0 1 ; uialign- 1]
                    uifill $sbpadhorizontal
                ] [uicolor 0x99202860 0 0 [uiclamp 1 1 1 1]]
                // used to toggle the background color of each row
                UI_sbtc = 0
                loopscoreboard cn 1 [
                    UI_sbtc = (! $UI_sbtc)
                    uitablerow [
                        uifill $sbpadhorizontal
                        if $scoreboardmultiplayer [
                            if $showclientnum [uifill $sbcolumnwidth $rowheight [uicolortext (? (isai $cn) "^f4-" $cn)                  $color_mid_gray $sbmidtext ; uialign- -1]]
                            if $showping      [uifill $sbcolumnwidth $rowheight [uicolortext (? (isai $cn) "^f4-" (scoreboardping $cn)) $sbnetstat $sbmidtext ; uialign- -1]]
                            if $showpj        [uifill $sbcolumnwidth $rowheight [uicolortext (? (isai $cn) "^f4-" (scoreboardpj $cn))   $sbnetstat $sbmidtext ; uialign- -1]]
                        ]
                        if $showdeaths       [uifill $sbcolumnwidth  $rowheight [uicolortext (getclientdeaths $cn)                      $sbdeaths $sbmidtext ; uialign- -1]]
                        if $showkills        [uifill $sbcolumnwidth  $rowheight [uicolortext (getclientfrags  $cn)                      $sbkills $sbmidtext ; uialign- -1]]
                        if $showscore        [uifill $sbcolumnwidth  $rowheight [uicolortext (getclientflags  $cn)                      $sbscore $sbmidtext ; uialign- -1]]
                        uifill (sbnamecolwidth) $rowheight [uicolortext (getclientcolorname $cn) (scoreboardstatus $cn) $sbbigtext ; uialign- 1]
                        uifill $sbpadhorizontal
                    ] [
                        uicolor (? $UI_sbtc $sbtablerow1 $sbtablerow2) 0 0 [uiclamp 1 1 1 1]
                        pushif highlight (scoreboardhighlight $cn) [uioutline $color_mid_gray (-f (*f $sbwidth 0.5) $playeroutlinewidth) (-f $rowheight $playeroutlinewidth)]
                    ]
                ]
            ]
            uitable 0 0 [
                uialign 0 -1
                uitableheader [
                    uifill $sbpadhorizontal
                    uifill (sbnamecolwidth) $sbtableheaderheight [uicolortext (getteamscore 2) 0xD04040 1 ; uialign- -1]
                    if $showscore        [uifill $sbcolumnwidth  $sbtableheaderheight [uicolortext "Score" $sbscore $sblittletext ; uialign-  1 1]]
                    if $showkills        [uifill $sbcolumnwidth  $sbtableheaderheight [uicolortext "K"     $sbkills $sblittletext ; uialign-  1 1]]
                    if $showdeaths       [uifill $sbcolumnwidth  $sbtableheaderheight [uicolortext "D"     $sbdeaths $sblittletext ; uialign-  1 1]]
                    if $scoreboardmultiplayer [
                        if $showpj        [uifill $sbcolumnwidth $sbtableheaderheight [uicolortext "PJ"    $sbnetstat $sblittletext ; uialign-  1 1]]
                        if $showping      [uifill $sbcolumnwidth $sbtableheaderheight [uicolortext "P"     $sbnetstat $sblittletext ; uialign-  1 1]]
                        if $showclientnum [uifill $sbcolumnwidth $sbtableheaderheight [uicolortext "#"     $color_mid_gray $sblittletext ; uialign-  1 1]]
                    ]
                    uifill $sbpadhorizontal
                ] [uicolor 0x99602020 0 0 [uiclamp 1 1 1 1]]
                // used to toggle the background color of each row
                UI_sbtc = 0
                loopscoreboard cn 2 [
                    UI_sbtc = (! $UI_sbtc)
                    uitablerow [
                        uifill $sbpadhorizontal
                        uifill (sbnamecolwidth) $rowheight [uicolortext (getclientcolorname $cn) (scoreboardstatus $cn) $sbbigtext ; uialign- -1]
                        if $showscore        [uifill $sbcolumnwidth  $rowheight [uicolortext (getclientflags  $cn)                      $sbscore $sbmidtext ; uialign-  1]]
                        if $showkills        [uifill $sbcolumnwidth  $rowheight [uicolortext (getclientfrags  $cn)                      $sbkills $sbmidtext ; uialign-  1]]
                        if $showdeaths       [uifill $sbcolumnwidth  $rowheight [uicolortext (getclientdeaths $cn)                      $sbdeaths $sbmidtext ; uialign-  1]]
                        if $scoreboardmultiplayer [
                            if $showpj        [uifill $sbcolumnwidth $rowheight [uicolortext (? (isai $cn) "^f4-" (scoreboardpj $cn))   $sbnetstat $sbmidtext ; uialign-  1]]
                            if $showping      [uifill $sbcolumnwidth $rowheight [uicolortext (? (isai $cn) "^f4-" (scoreboardping $cn)) $sbnetstat $sbmidtext ; uialign-  1]]
                            if $showclientnum [uifill $sbcolumnwidth $rowheight [uicolortext (? (isai $cn) "^f4-" $cn)                  $color_mid_gray $sbmidtext ; uialign-  1]]
                        ]
                        uifill $sbpadhorizontal
                    ] [
                        uicolor (? $UI_sbtc $sbtablerow1 $sbtablerow2) 0 0 [uiclamp 1 1 1 1]
                        pushif highlight (scoreboardhighlight $cn) [uioutline $color_mid_gray (-f (*f $sbwidth 0.5) $playeroutlinewidth) (-f $rowheight $playeroutlinewidth)]
                    ]
                ]
            ]
        ]
    ]
]
//-------------------------------------------------------------------------------------------------
UI_spectatorlist = [
    uicolor (? $sbtransparent $c_menu_a $c_menu) 0 $rowheight [
        uiclamp 1 1 1 1
        if $showspectators [
            uigrid (? (> $getmode 0) 3 2) 0 0 [
                loopscoreboard cn -1 [
                    uispace $pad_thin $pad_thin [
                        pushif sbhigh (scoreboardhighlight $cn) [
                            uioutline $color_mid_gray ; uiclamp- 1 1 1 1
                        ]
                        uihlist $pad_thin [
                            uicolortext (getclientcolorname $cn) (scoreboardstatus $cn) $sbbigtext
                            if $scoreboardmultiplayer [if $showclientnum [uitext $cn $sbbigtext]]
                        ]
                    ]
                ]
            ]
        ]
    ]
]
