///////////////////////////////////////////////////////////////////////////////
//  Server Browser                                                           //
///////////////////////////////////////////////////////////////////////////////

_sb_mm = 0
_sb_mode = 0
_sb_ping = 9999
_sb_players = -1
_sb_sort = 1
_sb_showwait = 1

// quickfilterlist ["NAME" [children] [action] ..] <COLUMNS> <X PADDING> <Y PADDING> <BUTTON WIDTH> <BUTTON HEIGHT>
quickfilterlist = [
    uigrid $arg2 $arg3 $arg4 [
        looplist3 n c a $arg1 [
            if (!=s $n "") [
                UIbutton "hold2" [
                    c ; uitext $n 0.6
                ] $arg5 $arg6 $a -1
            ] [uifill $arg5 $arg6]
        ]
    ]
]

UImenu "server_browser" [
    refreshservers
    uivlist 0.01 [
        uihlist 0 [
            uiclamp 1 1
            UIbutton "" [uitext $uistr_serverbrowser_0 0.6] 0.2 0.032 [updatefrommaster] 0 [uialign -1 0]
            uihlist 0 [
                UIbutton "" [
                    UIcheckbox $searchlan 0.016
                    uitext $uistr_serverbrowser_1 0.6
                ] 0.18 0.032 [searchlan (! $searchlan)] -1
                UIbutton "" [
                    UIcheckbox $autoupdateservers 0.016
                    uitext $uistr_serverbrowser_2 0.6
                ] 0.18 0.032 [autoupdateservers (! $autoupdateservers)] -1
                UIbutton "" [
                    UIcheckbox $autosortservers 0.016
                    uitext $uistr_serverbrowser_3 0.6
                ] 0.18 0.032 [
                    _sb_sort = (! $_sb_sort)
                    autosortservers $_sb_sort
                ] -1
            ]
        ]
        uihlist 0.01 [
            uialign 1 0
            uiscroll 1.1 0.65 [
                uitarget 1.07 0.65 [
                    if $_sb_sort [autosortservers (! (uihover?))]
                    uivlist 0.0035 [
                        uialign 0 -1
                        uiclamp 1 1
                        loop i $numservers [
                            if (servinfovalid $i) [
                                if (&& [
                                    <= (servinfoping $i) $_sb_ping
                                ] [
                                    ! (& (<< 1 (servinfomastermode $i)) $_sb_mm)
                                ] [
                                    ! (& (<< 1 (servinfomode $i)) $_sb_mode)
                                ] [
                                    != (servinfonumplayers $i) (* $_sb_players (servinfomaxplayers $i))
                                ]) [
                                    _servers_map = (servinfomap $i)
                                    UIbuttonarea "" 0 0.065 [
                                        uirelease [connectservinfo $i]
                                        uispace 0.01 0 [
                                            uiclamp 1 1
                                            uihlist 0.01 [
                                                uialign -1
                                                uifill 0.09 0 [
                                                    uivlist 0.002 [
                                                        uihlist 0.006 [
                                                            uitext (servinfonumplayers $i) 0.8
                                                            uitext "/" 0.6
                                                            uitext (servinfomaxplayers $i) 0.6
                                                        ]
                                                        uihlist 0.006 [
                                                            uitext (servinfoping $i) 0.6
                                                            uitext "ms" 0.55
                                                        ]
                                                    ]
                                                ]
                                                uifill 0.68 0 [
                                                    uiclip 0.68 0 [
                                                        uialign -1
                                                        uivlist 0.002 [
                                                            uispace 0.01 0 [uitext (servinfodesc $i) 0.65]
                                                            uialign- -1
                                                            uihlist 0.015 [
                                                                uialign -1
                                                                uifill 0.012
                                                                uihlist 0.005 [
                                                                    uitext "^fs[" 0.6
                                                                    uitext (servinfomastermodename $i) 0.6
                                                                    uitext "^fS]" 0.6
                                                                ]
                                                                if (!=s $_servers_map "") [
                                                                    UItriangle 0x404040 0.01 0.01 270
                                                                    uitext $_servers_map 0.6
                                                                ]
                                                                if (MODE_TIMED (servinfomode $i)) [
                                                                    UItriangle 0x404040 0.01 0.01 270
                                                                    uitext (concatword "^f8" (servinfotime $i)) 0.6
                                                                ]
                                                            ]
                                                        ]
                                                    ]
                                                    UIbar 0 1 ; uialign- 1
                                                ]
                                            ]
                                            uihlist 0.005 [
                                                uialign 1
                                                uivlist 0.004 [
                                                    uicolortext (servinfoname $i) 0x808080 0.55
                                                    uicolortext (servinfoport $i) 0x808080 0.55
                                                    uialign* 1
                                                ]
                                                if (&& [=s $connectedip (servinfoname $i)] [= $connectedport (servinfoport $i)]) [
                                                    UItriangle 0xCCCCCC 0.04 0.015 90
                                                ] [uifill 0.015 0]
                                            ]
                                        ]
                                    ]
                                ]
                            ] [
                                if $_sb_showwait [
                                    // could offer customised messages one day
                                    UIbutton "" [uitext (servinfodesc $i) 0.6] 0 0.065
                                ]
                            ]
                        ]
                        uiclamp* 1 1
                    ]
                ]
            ]
            UIvscroll 0.02 0.65 1.3
        ]
    ]
] [initservers] [] [] $uistr_main_0
