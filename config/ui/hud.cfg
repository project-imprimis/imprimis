///////////////////////////////////////////////////////////////////////////////
//  HUD Elements                                                             //
///////////////////////////////////////////////////////////////////////////////

hudbgcolor = 0x99000000

resethud = [
    if (! $mainmenu) [
        if $editing [ //if ingame and in edit mode
            showui "editstats"
            showui "varicons"
            hideui "hudfill"
            hideui "hud"
            hideui "gamebar"
            hideui "weaponbar"
            hideui "centerbar"
        ] [ //if ingame and not in edit mode
            showui "hudfill"
            showui "hud"
            showui "gamebar"
            showui "weaponbar"
            showui "centerbar"
            hideui "editstats"
            hideui "varicons"
        ]
    ] [ //if on the main menu hide everything
        hideui "hudfill"
        hideui "hud"
        hideui "gamebar"
        hideui "weaponbar"
        hideui "centerbar"
        hideui "editstats"
        hideui "varicons"
    ]
]

newui "hudfill" [
    uiallowinput 0
    if $showhud [
        uialign 1 1
        uispace 0 0 [
            uivlist 0 [
                uihlist 0 [
                    uicolor $hudbgcolor 0.475 0.075
                    uitriangle $hudbgcolor 0.1052 0.0526 135
                    uifill 0.7
                    uitriangle $hudbgcolor 0.1052 0.0526 225
                    uicolor $hudbgcolor 0.475 0.075
                ]
            ]
        ]
    ]
]

newui "hud" [
    uiallowinput 0
    if ( = (getclientteam (getclientnum)) 0) [
        hudbgcolor = 0x99000000
    ]
    if ( = (getclientteam (getclientnum)) 1) [
        hudbgcolor = 0x99000044
    ]
    if ( = (getclientteam (getclientnum)) 2) [
        hudbgcolor = 0x99440000
    ]
    if $showhud [
        uialign -1 1
        uiabovehud
        uispace 0.02 0.017 [
            uivlist 0.01 [
                //uialign- -1 0
                //how this works: players have 10 hp so 10 individual uifills are created either
                //white (for hitpoints the player still has) or gray (for hitpoints lost)
                uihlist 0.005 [
                    uicolortext (format "%1" (max 0 (getclienthealth (getclientnum)))) 0xFFFFFF 0.7
                    uifill 0.005
                    loop i 10 [
                        if (> (getclienthealth (getclientnum)) $i) [
                            uicolor 0xFFFFFF 0.029 0.004 [
                                uioutline 0xFFFFFF ; uiclamp- 1 1 1 1
                            ]
                        ] [
                            uicolor 0xAA505050 0.029 0.004 [
                                uioutline 0xAA505050 ; uiclamp- 1 1 1 1
                            ]
                        ]
                    ]
                ]
            ]
        ]
    ]
]

newui "gamebar" [
    uiallowinput 0
    if $showhud [
        uialign 0 -1
        uivlist 0 [
            uihlist 0.0 [
                uitriangle $hudbgcolor 0.1052 0.0526 315
                uicolor $hudbgcolor 0.1 0.075 [
                    uivlist -0.005 [
                        uicolortext $scoreboardtime 0xBBBBBB 0.7
                        uihlist 0 [
                            uivlist 0 [
                                uifill 0.04 0
                                uicolortext (getteamscore 2) 0xF07050 1.1
                            ]
                            uivlist 0 [
                                uifill 0 0
                                uitext " - " 0.8
                            ]
                            uivlist 0 [
                                uifill 0.04 0
                                uicolortext (getteamscore 1) 0x5070F0 1.1
                            ]
                        ]
                        uicolortext "10" 0xAAAAAA 0.5
                    ]
                ]
                uitriangle $hudbgcolor 0.1052 0.0526 45
            ]
        ]
    ]
]

cooldownbar = [
    barcolor = 0x33FFFFFF
    if (<f (getweaponheat) 0.3) [
        barcolor = 0x01FFFFFF
    ] [
        if (<f (getweaponheat) 0.6) [
            barcolor = 0x11FFCCCC
        ] [
            if (<f (getweaponheat) 0.8) [
                barcolor = 0x22FF7999
            ] [
                if (<f (getweaponheat) 0.9) [
                    barcolor = 0x2CFF6666
                ] [
                    barcolor = 0x33FF0000
                ]
            ]
        ]
    ]
    uicircle $barcolor 0.06
]

newui "centerbar" [
    uiallowinput 0
    if $showhud [
        uialign 0 0
        uispace 0 0 [
            uivlist 0 [

                cooldownbar
            ]
        ]
    ]
]

//convenience body of carbine rendering code, as it gets called twice
//it will be called early if some other weapon is out, and late if it is out
//this allows the large weapon (see fontsize, imgx,imgy) to always be in the same location
carbine = [
    if (!= (getweapon) 4) [
        fontsize = 0.01
        imgx = 0.08
        imgy = 0.025
        fill = 0.05
    ] [
        fontsize = 0.5
        imgx = 0.25
        imgy = 0.06
        fill = $UIwindow_padding
    ]
    uialign -1 1
    uispace $UIwindow_padding 0 [
        uialign -1 1
        uihlist -0.03 [
            uiimage "media/interface/weapon/carbineicon.png" $imgx $imgy
            uitext $uistr_weap_0 $fontsize [uialign 1 1]
            uifill $fill
        ]
    ]
]

//bottom right weapon bar readout, shows the two weapons in player's possession
newui "weaponbar" [
    uiallowinput 0
    if $showhud [
        uialign 1 1
        uispace $UIwindow_padding $UIwindow_padding [
            uialign -1 -1
            uihlist $UIwindow_padding [
                if (!= (getweapon) 4) [ //render carbine first if it is not being wielded (needs to be small)
                    (carbine)
                ]
                //set appropriate sizes for main weapon (small if not wielded, large if wielded)
                if (= (getweapon) 4) [
                    fontsize = 0.01
                    imgx = 0.08
                    imgy = 0.025
                    fill = 0.05
                ] [
                    fontsize = 0.5
                    imgx = 0.25
                    imgy = 0.06
                    fill = $UIwindow_padding
                ]
                uialign -1 1
                uispace $UIwindow_padding 0 [
                    uialign -1 1
                    uihlist -.03 [
                        if (= (getcombatclass) 0) [
                            uiimage "media/interface/weapon/rifleicon.png" $imgx $imgy
                            uitext $uistr_weap_1 $fontsize [uialign 1 1]
                        ]
                        if (= (getcombatclass) 1) [
                            uiimage "media/interface/weapon/pulseicon.png" $imgx $imgy
                            uitext $uistr_weap_2 $fontsize [uialign 1 1]
                        ]
                        if (= (getcombatclass) 2) [
                            uiimage "media/interface/weapon/pulseicon.png" $imgx $imgy
                            uitext $uistr_weap_3 $fontsize [uialign 1 1]
                        ]
                        if (= (getcombatclass) 3) [
                            uiimage "media/interface/weapon/scattergunicon.png" $imgx $imgy
                            uitext $uistr_weap_4 $fontsize [uialign 1 1]
                        ]
                        uifill $fill
                    ]
                ]
                if (= (getweapon) 4) [ //render carbine later if it is being weilded (so the small primary weapon is on the left)
                    (carbine)
                ]
            ]
        ]
    ]
]

newui "editstats" [
    uiallowinput 0
    if $showhud [
        uialign -1 1
        uiabovehud
        uicolor $c_menu_a 0 0 [
            uispace $UIwindow_padding $UIwindow_padding [
                uivlist 0 [
                    uihlist 0 [
                        uicontextfill 18 0 [uicontext (format "%1 %2" $uistr_hud_4 $gridpower)    ; uialign- -1 0]
                        uicontextfill 18 0 [uicontext (format "%1 %2" $uistr_hud_5 (mapsize))     ; uialign- -1 0]
                        uicontextfill 18 0 [uicontext (format "%1 %2" $uistr_hud_6 $floatspeed)   ; uialign- -1 0]
                        uicontextfill 12 0 [uicontext (format (concatword $uistr_hud_10 " %1%2") (selchildnum) (if $showmat [selchildmat ": "])); uialign- -1 0]
                    ]
                    uihlist 0 [
                        uicontextfill 18 0 [uicontext (format "wtr:%1 (%2%%)" (wtr) (vtr))    ; uialign- -1 0]
                        uicontextfill 18 0 [uicontext (format "wvt:%1 (%2%%)" (wvt) (vvt))    ; uialign- -1 0]
                        uicontextfill 18 0 [uicontext (format "evt:%1 " (evt))                       ; uialign- -1 0]
                        uicontextfill 12 0 [uicontext (format "eva:%1 " (eva))                       ; uialign- -1 0]
                    ]
                    uihlist 0 [
                        uicontextfill 12 0 [uicontext (concatword "ond:" (octa))                     ; uialign- -1 0]
                        uicontextfill 12 0 [uicontext (concatword "va:" (va))                        ; uialign- -1 0]
                        uicontextfill 12 0 [uicontext (format "gl:%1(%2)" (gldes) (geombatch))       ; uialign- -1 0]
                        uicontextfill 12 0 [uicontext (concatword "oq:" (oq))                        ; uialign- -1 0]
                    ]
                    uialign* -1 0
                ]
            ]
            uioutline $c_line ; uiclamp- 1 1 1 1
        ]
    ]
]

newui "varicons" [
    uiallowinput 0
    if $showhud [
        uialign 1 1
        uispace 0.015 0.1 [
            uivlist 0.005 [
                col = "0xFFFFFF"
                if ($allfaces) [col =  $edithighlightcol] [col = "0xFFFFFF"]
                uicolortext $uistr_hud_11 $col 0.5
                if ($entselsnap) [col =  $edithighlightcol] [col = "0xFFFFFF"]
                uicolortext $uistr_hud_12 $col 0.5
                if ($entediting) [col =  $edithighlightcol] [col = "0xFFFFFF"]
                uicolortext $uistr_hud_13 $col 0.5
                if ($fullbright) [col = $edithighlightcol] [col = "0xFFFFFF"]
                uicolortext $uistr_hud_14 $col 0.5
                if ($showmat) [col =  $edithighlightcol] [col = "0xFFFFFF"]
                uicolortext $uistr_hud_15 $col 0.5
                if ($nompedit) [col =  $edithighlightcol] [col = "0xFFFFFF"]
                uicolortext $uistr_hud_16 $col 0.5
            ]
        ]
    ]
]
