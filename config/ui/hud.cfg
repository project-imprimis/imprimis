///////////////////////////////////////////////////////////////////////////////
//  HUD Elements                                                             //
///////////////////////////////////////////////////////////////////////////////
resethud = [
    if (! $mainmenu) [
        if $editing [ //if ingame and in edit mode
            showui "editstats"
            showui "varicons"
            hideui "hud"
            hideui "gamebar"
            hideui "weaponbar"
        ] [ //if ingame and not in edit mode
            showui "hud"
            showui "gamebar"
            showui "weaponbar"
            hideui "editstats"
            hideui "varicons"
        ]
    ] [ //if on the main menu hide everything
        hideui "hud"
        hideui "gamebar"
        hideui "weaponbar"
        hideui "editstats"
        hideui "varicons"
    ]
]

newui "hud" [
    uiallowinput 0
    if $showhud [
        uialign -1 1
        uiabovehud
        uicolor $c_menu_a 0 0 [
            uispace $UIwindow_padding $UIwindow_padding [
                //how this works: players have 10 hp so 10 individual uifills are created either
                //white (for hitpoints the player still has) or black (for hitpoints lost)
                uivlist 0 [
                    uitext (format "%1" (max 0 (getclienthealth (getclientnum)))) 1
                    loop i 10 [
                        //note getting reverse by subtracting i from 10
                        if (>= (getclienthealth (getclientnum)) (- 10 $i)) [
                            uicolor 0xFFFFFF 0.05 0.015
                        ] [
                            uicolor 0x000000 0.05 0.015
                        ]
                    ]
                ]
            ]
        ]
    ]
]

newui "gamebar" [
    uiallowinput 0
    if $showhud [
        uialign 0 -1
        uicolor $c_menu_a 1 0 [
            uispace $UIwindow_padding $UIwindow_padding [
                uihlist 0 [
                    //difference between two teams score, normalized to the end of match points so it fits on a bar nicely
                    score = (divf (*f 0.5 (abs (format "%1" (- (getteamscore 2) (getteamscore 1))))) 500)
                    sign = (> (- (getteamscore 2) (getteamscore 1)) 0)
                    if (sign) [
                        uicolor $c_menu_a $score 0.03
                        uicolor 0xFF0000 $score 0.03
                    ] [
                        uicolor 0x0000FF $score 0.03
                        uicolor $c_menu_a $score 0.03
                    ]
                ]
            ]
        ]
    ]
]

//convenience body of carbine rendering code, as it gets called twice
//it will be called early if some other weapon is out, and late if it is out
//this allows the large weapon (see fontsize, imgx,imgy) to always be in the same location
carbine = [
    if (!= (getweapon) 3) [
        fontsize = 0.01
        imgx = 0.08
        imgy = 0.025
    ] [
        fontsize = 0.7
        imgx = 0.32
        imgy = 0.1
    ]
    uialign -1 1
    uispace $UIwindow_padding $UIwindow_padding [
        uialign -1 1
        uivlist 0 [
            uitext "Carbine" $fontsize
            uiimage "media/interface/weapon/carbineicon.png" $imgx $imgy
        ]
    ]
]

//bottom right weapon bar readout, shows the two weapons in player's possession
newui "weaponbar" [
    uiallowinput 0
    if $showhud [
        uialign 1 1
        uispace $UIwindow_padding $UIwindow_padding [
            uialign -1 1
            uihlist $UIwindow_padding [
                if (!= (getweapon) 3) [ //render carbine first if it is not being wielded (needs to be small)
                    (carbine)
                ]
                //set appropriate sizes for main weapon (small if not wielded, large if wielded)
                if (= (getweapon) 3) [
                    fontsize = 0.01
                    imgx = 0.08
                    imgy = 0.025
                ] [
                    fontsize = 0.7
                    imgx = 0.32
                    imgy = 0.1
                ]
                uialign -1 1
                uispace $UIwindow_padding $UIwindow_padding [
                    uialign -1 1
                    uivlist 0 [
                        if (= (getcombatclass) 0) [
                            uitext "Railgun" $fontsize
                            uiimage "media/interface/weapon/rifleicon.png" $imgx $imgy
                        ]
                        if (= (getcombatclass) 1) [
                            uitext "Demo Gun" $fontsize
                            uiimage "media/interface/weapon/pulseicon.png" $imgx $imgy
                        ]
                        if (= (getcombatclass) 2) [
                            uitext "Block Layer" $fontsize
                            uiimage "media/interface/weapon/pulseicon.png" $imgx $imgy
                        ]
                    ]
                ]
                if (= (getweapon) 3) [ //render carbine later if it is being weilded (so the small primary weapon is on the left)
                    (carbine)
                ]
            ]
        ]
    ]
]

newui "editstats" [
    uiallowinput 0
    if $showhud [
        uialign -1 1
        uiabovehud
        uicolor $c_menu_a 0 0 [
            uispace $UIwindow_padding $UIwindow_padding [
                uivlist 0 [
                    uicontext (
                        format "%4^f7%5^f7gridsize :^f8 %1 ^f7| worldsize :^f8 %2 ^f7| floatspeed :^f8 %3" $gridpower $mapscale $floatspeed (
                            if $hmapedit [format "heightmap brush^f8 %1 ^f7:^f8 %2^n" $hbrushindex $hbrushname] [
                                if (! (enthavesel)) [
                                format "index^f8 %1 ^f7:^f8 %2^n" (max 0 (getseltex)) (gettexname (getseltex))
                                ]
                            ]
                        ) (if (enthavesel) [concat (entget) ":^f8" (enthavesel) "^f7selected^n"])
                    )
                    uicontext (format "cube %1%2" $selchildcount (if $showmat [selchildmat ": "]))
                    uihlist 0 [
                        uicontextfill 16 0 [uicontext (format "wtr:%1 (%2%%)" $editstatwtr $editstatvtr)    ; uialign- -1 0]
                        uicontextfill 16 0 [uicontext (format "wvt:%1 (%2%%)" $editstatwvt $editstatvvt)    ; uialign- -1 0]
                    ]
                    uihlist 0 [
                        uicontextfill 16 0 [uicontext (format "evt:%1 " $editstatevt)                       ; uialign- -1 0]
                        uicontextfill 16 0 [uicontext (format "eva:%1 " $editstateva)                       ; uialign- -1 0]
                    ]
                    uihlist 0 [
                        uicontextfill 12 0 [uicontext (concatword "ond:" $editstatocta)                     ; uialign- -1 0]
                        uicontextfill 12 0 [uicontext (concatword "va:" $editstatva)                        ; uialign- -1 0]
                        uicontextfill 11 0 [uicontext (format "gl:%1(%2)" $editstatglde $editstatgeombatch) ; uialign- -1 0]
                        uicontextfill  7 0 [uicontext (concatword "oq:" $editstatoq)                        ; uialign- -1 0]
                    ]
                    uialign* -1 0
                ]
            ]
            uioutline $c_line ; uiclamp- 1 1 1 1
        ]
    ]
]

newui "varicons" [
    uiallowinput 0
    if $showhud [
        uialign 1 1
        uispace 0.01 0.1 [
            uivlist 0 [
                looplist i (concatword "allfaces entselsnap entediting fullbright showmat " (? $scoreboardmultiplayer "nompedit")) [
                    uifont "default_outline" [
                        uialign 1
                        if $$i [
                            uitext (concatword "^f8" $i) 0.47
                        ] [uicolortext $i 0x80FFFFFF 0.47]
                    ]
                ]
            ]
        ]
    ]
]
